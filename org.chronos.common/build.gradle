// ORG.CHRONOS.COMMON/build.gradle

jar {
    manifest {
        attributes 'Implementation-Title': 'Chronos Common'
    }
}

dependencies {
	// Utilities
    compile("com.esotericsoftware:kryo:${project.kryoVersion}");
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier "sources"
}

// Defines the to-be published artifacts with the maven-publish task
// Is not responsible for any publishing. Just defines the artifacts.
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourceJar
			//the maven descriptor info
			groupId project.group
			artifactId jar.baseName
			version project.mavenVersion
		}
	}

}

task generateBuildInfoFile << {
	File configFile = file('src/main/resources/buildInfo.properties')
	System.out.println("Creating build info file (${project.version}) in: " + configFile.getAbsolutePath());
	if(configFile.exists()){
		configFile.delete();
	}
	configFile.createNewFile();
	Properties properties = new Properties();
	properties.setProperty("org.chronos.buildVersion", project.version);
	FileOutputStream fos = new FileOutputStream(configFile);
	try{	
		fos.write("# GENERATED BY CHRONOS BUILD SCRIPT\n\n".getBytes());
		properties.store(fos, null);
	}finally{
		fos.close();
	}
}

// generate the buildInfo.properties file before compiling the classes
compileJava {
	dependsOn generateBuildInfoFile
}

compileTestJava {
	dependsOn generateBuildInfoFile
}
